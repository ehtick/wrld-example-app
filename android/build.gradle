apply plugin: 'com.android.application'

buildscript {
    repositories {
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

allprojects {
    repositories {
        google()
        gradlePluginPortal()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url('file://' + new File('libs/senion-ips-6.2.0-AAR').absolutePath)
        }
        maven {
            url "https://dl.cloudsmith.io/public/indooratlas/mvn-public/maven/"
        }
    }
}

task wrapper(type: Wrapper) {
    // generate gradlew wrapper files for specified version of gradle
    gradleVersion = '4.6'
}


def getVersionCodeFromProjectProperty = { ->
    def code = project.hasProperty('versionCode') ? versionCode.toInteger() : 1
    logger.lifecycle("versionCode is set to $code")
    return code
}

def getVersionNameFromProjectProperty = { ->
    def name = project.hasProperty('versionName') ? versionName : "1.0"
    logger.lifecycle("versionName is set to $name")
    return name
}

def getApplicationIdFromProjectProperty = { ->
    def applicationId = project.hasProperty('applicationId') ? applicationId : "com.eegeo.mobileexampleapp"
    logger.lifecycle("applicationId is set to $applicationId")
    return applicationId
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId getApplicationIdFromProjectProperty()
        minSdkVersion 21
        targetSdkVersion 29
        multiDexEnabled true
        versionCode getVersionCodeFromProjectProperty()
        versionName getVersionNameFromProjectProperty()

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11",
                    "-frtti",
                    "-fexceptions",
                    "-Werror",
                    "-Wall",
                    "-Wno-reorder"

                arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_static"
            }

            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

        androidTest {
            java.srcDirs = ['tests']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            externalNativeBuild {
                cmake {
                    cppFlags "-g"
                    arguments "-DDEBUG=1"
                }
            }

            jniDebuggable true
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "main"

    productFlavors {
        normal {
            dimension "main"
        }

        screenshot {
            dimension "main"

            externalNativeBuild {
                cmake {
                   cppFlags "-DAUTOMATED_SCREENSHOTS=1"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            // Set path to external eegeo-mobile top CMakeLists here and in sdk/CMakeLists.txt
            path "CMakeLists.txt"
        }
    }
}

if (project.hasProperty("external.signing")) {

    def signingGradle = project.property("external.signing") + ".gradle"
    if (new File(signingGradle).exists()) {
        logger.lifecycle("applying signing properties from " + signingGradle)
        apply from: signingGradle
    }
    else {
        throw new InvalidUserDataException('Project parameter external.signing supplied but gradle file does not exist at path relative to project: ' + signingGradlePath)
    }
} else {
    logger.lifecycle("no external.signing property supplied." )
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    androidTestImplementation 'tools.fastlane:screengrab:1.0.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'

    implementation 'androidx.multidex:multidex:2.0.0'

    implementation ('androidx.appcompat:appcompat:1.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation project(':wrld-android-search-widget-release')
    implementation project(':wrld-navigation-release')
    implementation project(':wrld-navigation-widget-release')
    implementation project(':wrld-navigation-search-release')
    implementation 'com.senion.ips:senion-ips:6.2.0'
    implementation 'com.google.android.gms:play-services:12.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'com.indooratlas.android:indooratlas-android-sdk:3.4.0@aar'
    implementation 'com.microsoft.appcenter:appcenter-crashes:4.1.0'
}
